<!DOCTYPE html>
<html>
	<head>
		<meta charset="utf-8"/>
		<title>Zettel 5 - Aufgabe 1</title>
		
		<!--
			Light Mode 1
			============
			Lighting using the Vertex Shader
		-->
		<script id="vertex-shader-light-1" type="x-shader/x-vertex">
			attribute vec4 vPosition;
			attribute vec4 vColor;
			attribute vec4 vNormal;
			
			uniform mat4 modelMatrix;
			uniform mat4 viewMatrix;
			uniform mat4 projectionMatrix;
			uniform mat4 normalMatrix;

			uniform vec3 lightPos;
			uniform vec4 diffuseColor;
			uniform vec4 specularColor;

			uniform float ambientIntensity; // I_a
			uniform float diffuseIntensity; // I_d
			uniform float specularItensity; // I_s
			uniform float specularExponent; // n_s

			const float c_1 = 0.1;
			const float c_2 = 0.67;
			const float c_3 = 0.1;

			varying vec4 fColor;

			void main()
			{
				fColor = vColor;
				gl_Position = projectionMatrix * viewMatrix * modelMatrix * vPosition;

				// Let it be light!
				vec3 normal = vec3(normalMatrix * vec4(vNormal));		// N
				vec4 vertPos4 = modelMatrix * vec4(vPosition);
				vec3 vertPos = vec3(vertPos4) / vertPos4.w;
				vec3 lightDir = normalize(lightPos - vertPos);			// L
				vec3 reflectDir = reflect(-lightDir, normal);			// R
				vec3 viewDir = normalize(-vertPos);						// V

				float distance = distance(vec4(lightPos, 1.0), vPosition);	// d

				// I_a * k_a
				vec4 ambientLight = ambientIntensity * vColor;

				// I_d * k_d * max(0, N * L)
				vec4 diffuseLight = diffuseIntensity * vColor * max(dot(normal, lightDir), 0.0);

				// I_s * k_s * max(0, R * V)^n
				vec4 specularLight = specularItensity * specularColor * pow(max(dot(reflectDir, viewDir), 0.0), specularExponent);

				// min(1 / (c1 + c2 * d + c3 * d^2), 1)
				float fAtt = min(1.0 / (c_1 + c_2 * distance + c_3 * pow(distance, 2.0)), 1.0);

				// Phong-Gleichung
				fColor = ambientLight + fAtt * (diffuseLight + specularLight);
			}
		</script>

		<script id="fragment-shader-light-1" type="x-shader/x-fragment">
			precision mediump float;
			
			varying vec4 fColor;

			void main()
			{
				gl_FragColor = fColor;
			}
		</script>

		<!--
			Light Mode 2
			============
			Lighting using the Fragment Shader
		-->
		<script id="vertex-shader-light-2" type="x-shader/x-vertex">
			attribute vec4 vPosition;
			attribute vec4 vColor;
			attribute vec4 vNormal;

			uniform mat4 modelMatrix;
			uniform mat4 viewMatrix;
			uniform mat4 projectionMatrix;
			uniform mat4 normalMatrix;

			varying vec4 fColor;
			varying vec3 normalInterpolated;
			varying vec3 vertPos;

			void main()
			{
				fColor = vColor;
				gl_Position = projectionMatrix * viewMatrix * modelMatrix * vPosition;

				vec4 vertPos4 = modelMatrix * vPosition;
				vertPos = vec3(vertPos4) / vertPos4.w;
				normalInterpolated = vec3(normalMatrix * vNormal);
			}
		</script>

		<script id="fragment-shader-light-2" type="x-shader/x-fragment">
			precision mediump float;

			uniform vec3 lightPos;
			uniform vec4 diffuseColor;
			uniform vec4 specularColor;

			uniform float ambientIntensity; // I_a
			uniform float diffuseIntensity; // I_d
			uniform float specularItensity; // I_s
			uniform float specularExponent; // n_s
			
			varying vec4 fColor;
			varying vec3 normalInterpolated;
			varying vec3 vertPos;

			void main()
			{
				gl_FragColor = fColor;

				vec3 normal = normalize(normalInterpolated);		// N
				vec3 lightDir = normalize(lightPos - vertPos);		// L
				vec3 reflectDir = reflect(-lightDir, normal);		// R
				vec3 viewDir = normalize(-vertPos);					// V

				// I_a * k_a
				vec4 ambientLight = ambientIntensity * fColor;

				// I_d * k_d * max(0, N * L)
				vec4 diffuseLight = diffuseIntensity * fColor * max(dot(normal, lightDir), 0.0);

				// I_s * k_s * max(0, R * V)^n
				vec4 specularLight = specularItensity * specularColor * pow(max(dot(reflectDir, viewDir), 0.0), specularExponent);

				// Phong-Gleichung
				gl_FragColor = ambientLight + diffuseLight + specularLight;
			}
		</script>

		<style>
			* {
				margin: 0;
				padding: 0;
			}

			html, body {
				width: 100%;
				height: 100%;
			}

			canvas {
				display: block;
			}
		</style>

		<script type="text/javascript" src="common/initShaders.js">
		</script>
		<script type="text/javascript" src="common/gl-matrix.js">
		</script>
	</head>
	<body>
		<canvas id="gl-canvas" width="512" height="512">
			If you see this, your browser doesn't support WebGL.
		</canvas>

		<script type="text/javascript" src="objects/WebGLObject.js"></script>
		<script type="text/javascript" src="objects/PalmTreeTrunk.js"></script>
		<script type="text/javascript" src="objects/PalmTreeLeaf.js"></script>
		<script type="text/javascript" src="objects/PalmTree.js"></script>
		<script type="text/javascript" src="objects/Island.js"></script>
		<script type="text/javascript" src="objects/Water.js"></script>
		
		<script type="text/javascript" src="utils/KeyHelper.js"></script>
		<script type="text/javascript" src="utils/FPSLimiter.js"></script>

		<script type="text/javascript" src="island.js"></script>
	</body>
</html>
